# Maintainer: AzPepoze <azpepoze@gmail.com>

_pkgname=linux-wallpaperengine-gui
pkgname=$_pkgname-git
pkgver=r52.28f1c71
pkgrel=1
pkgdesc="A graphical user interface for managing wallpapers on Linux, powered by Neutralinojs."
arch=('x86_64')
url="https://github.com/AzPepoze/linux-wallpaperengine-gui"
license=('MIT')
depends=('gtk3' 'webkit2gtk')
makedepends=('git' 'pnpm' 'npm')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+$url.git")
sha256sums=('SKIP')

pkgver() {
     cd $_pkgname
     printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$_pkgname"

    # Install dependencies
    cd app
    pnpm install
    cd ..

    # Build the application
    npx --yes @neutralinojs/neu build --release
}

package() {
    cd "$srcdir/$_pkgname"

    # Create the application directory
    install -d "$pkgdir/usr/lib/$_pkgname"

    # Application binary
    install -Dm755 "dist/$_pkgname/${_pkgname}-linux_x64" "$pkgdir/usr/lib/$_pkgname/$_pkgname"

    # Resources
    install -Dm644 "dist/$_pkgname/resources.neu" "$pkgdir/usr/lib/$_pkgname/resources.neu"

    # Wrapper script
    install -Dm755 /dev/stdin "$pkgdir/usr/bin/$_pkgname" <<EOF
#!/bin/sh
cd "/usr/lib/$_pkgname" && exec "./$_pkgname" "$@"
EOF

    # .desktop file
    install -Dm644 /dev/stdin "$pkgdir/usr/share/applications/$_pkgname.desktop" <<EOF
[Desktop Entry]
Name=Linux Wallpaper Engine GUI
Comment=A graphical user interface for managing wallpapers on Linux.
Exec=$_pkgname
Icon=$_pkgname
Terminal=false
Type=Application
Categories=Utility;
EOF

    # Icon
    install -Dm644 "app/public/icon.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"
}
